plugins {
    id 'java'
    id 'jacoco-report-aggregation'
}

repositories {
    mavenCentral()
}

dependencies {
    implementation project(':application')
    implementation project(':list')
    implementation project(':utilities')
}

allprojects {
    dependencyLocking {
        lockMode = LockMode.STRICT
        lockAllConfigurations()
    }
}

configurations {
    allCodeCoverageReportSourceDirectories {
        resolutionStrategy.deactivateDependencyLocking()
    }
    testCodeCoverageReportExecutionData {
        resolutionStrategy.deactivateDependencyLocking()
    }
}

tasks.named('check') {
    dependsOn tasks.named('testCodeCoverageReport', JacocoReport)
}


// Copied this task from here:
// https://docs.gradle.org/current/userguide/dependency_locking.html#generating_and_updating_dependency_locks
tasks.register('resolveAndLockAll') {
    doFirst {
        assert gradle.startParameter.writeDependencyLocks
    }
    doLast {
        configurations.findAll {
            // Add any custom filtering on the configurations to be resolved
            it.canBeResolved
        }.each { it.resolve() }
    }
}